#include "lib/horus.dl"

/* Call Stack Depth */
.decl CallStackDepth(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
CallStackDepth(hash, timestamp, step, caller, callee, amount) :-
  call(step, hash, "CALL", caller, callee, _, amount, depth, _),
  depth > 1024,
  transaction(hash, _, _, _, _, block),
  block(block, _, _, timestamp).
.output CallStackDepth

/* Reentrancy */
.decl Reentrancy(hash:symbol, timestamp:number, caller:Address, callee:Address, depth:number, amount:Value)
Reentrancy(hash, timestamp, caller, callee, depth1, amount) :-
  call(_, hash, "CALL", caller, callee, _, amount, depth1, 1),
  call(_, hash, "CALL", caller, callee, _, amount, depth2, 1),
  depth1 < depth2,
  !match("0", amount),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output Reentrancy

/* Cross-Function Reentrancy */
.decl CrossFunctionReentrancy(hash:symbol, timestamp:number, caller:Address, callee:Address, depth:number, amount:Value)
CrossFunctionReentrancy(hash, timestamp, callee, caller, depth2, amount2) :-
  call(_, hash, "CALL", caller, callee, input_data1, amount1, depth1, 1),
  call(_, hash, "CALL", callee, caller, _, amount2, depth2, 1),
  call(_, hash, "CALL", caller, callee, input_data3, amount1, depth3, 1),
  depth1 < depth2,
  depth2 < depth3,
  input_data1 != input_data3,
  !match("0", amount2),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output CrossFunctionReentrancy

/* Delegated Reentrancy */
.decl DelegatedReentrancy(hash:symbol, timestamp:number, caller:Address, callee:Address, depth:number, amount:Value)
DelegatedReentrancy(hash, timestamp, caller, callee1, depth2, amount2) :-
  call(_, hash, "DELEGATECALL", caller, callee1, input_data, amount1, depth1, 1),
  call(_, hash, "CALL", callee1, _, _, amount2, depth2, 1),
  call(_, hash, "DELEGATECALL", caller, callee1, input_data, amount1, depth3, 1),
  depth1 < depth2,
  depth2 < depth3,
  !match("0", amount2),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output DelegatedReentrancy

/* Create-Based Reentrancy */
.decl CreateBasedReentrancy(hash:symbol, timestamp:number, caller:Address, callee:Address, depth:number, amount:Value)
CreateBasedReentrancy(hash, timestamp, caller, callee, depth3, amount2) :-
  call(_, hash, "CREATE", caller, _, input_data, amount1, depth1, 1),
  call(_, hash, "CREATE", caller, callee, input_data, amount1, depth2, 1),
  call(_, hash, "CALL", caller, callee, _, amount2, depth3, 1),
  depth1 < depth2,
  depth2 = depth3,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output CreateBasedReentrancy

/* Unhandled Exception */
.decl UnhandledException(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
UnhandledException(hash, timestamp, step1, caller, callee, amount) :-
  call(step1, hash, "CALL", caller, callee, _, amount, _, 0),
  !match("0", amount),
  !used_in_condition(step1, hash),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output UnhandledException

/* Integer Overflow */
.decl IntegerOverflow(hash:symbol, timestamp:number, step:number, opcode:Opcode, first_operand:Value, second_operand:Value, arithmetic_result:Value, evm_result:Value, from:Address, to:Address, amount:Value)
IntegerOverflow(hash, timestamp, step2, opcode, first_operand, second_operand, arithmetic_result, evm_result, from, to, amount) :-
  opcode(step1, "CALLDATALOAD"),
  arithmetic(step2, opcode, first_operand, second_operand, arithmetic_result, evm_result),
  arithmetic_result != evm_result,
  match("ADD", opcode),
  data_flow(step1, step2),
  storage(step3, "SSTORE", _, hash, _, _, _),
  data_flow(step2, step3),
  transfer(step4, hash, from, to, amount),
  data_flow(step1, step4),
  from != to,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "CALLDATALOAD"),
  arithmetic(step2, opcode, first_operand, second_operand, arithmetic_result, evm_result),
  arithmetic_result != evm_result,
  match("ADD", opcode),
  data_flow(step1, step2),
  call(step3, hash, "CALL", from, to, _, amount, _, 1),
  data_flow(step2, step3),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "CALLDATALOAD"),
  arithmetic(step2, opcode, first_operand, second_operand, arithmetic_result, evm_result),
  arithmetic_result != evm_result,
  match("MUL", opcode),
  data_flow(step1, step2),
  storage(step3, "SSTORE", _, hash, _, _, _),
  data_flow(step2, step3),
  transfer(step4, hash, from, to, amount),
  data_flow(step1, step4),
  from != to,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "CALLDATALOAD"),
  arithmetic(step2, opcode, first_operand, second_operand, arithmetic_result, evm_result),
  arithmetic_result != evm_result,
  match("MUL", opcode),
  data_flow(step1, step2),
  call(step3, hash, "CALL", from, to, _, amount, _, 1),
  data_flow(step2, step3),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output IntegerOverflow

/* Integer Underflow */
.decl IntegerUnderflow(hash:symbol, timestamp:number, step:number, first_operand:Value, second_operand:Value, arithmetic_result:Value, evm_result:Value, from:Address, to:Address, amount:Value)
IntegerUnderflow(hash, timestamp, step2, first_operand, second_operand, arithmetic_result, evm_result, from, to, amount) :-
  opcode(step1, "CALLDATALOAD"),
  arithmetic(step2, "SUB", first_operand, second_operand, arithmetic_result, evm_result),
  arithmetic_result != evm_result,
  data_flow(step1, step2),
  storage(step3, "SSTORE", _, hash, _, _, _),
  data_flow(step2, step3),
  transfer(step4, hash, from, to, amount),
  data_flow(step1, step4),
  from != to,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "CALLDATALOAD"),
  arithmetic(step2, "SUB", first_operand, second_operand, arithmetic_result, evm_result),
  arithmetic_result != evm_result,
  data_flow(step1, step2),
  call(step3, hash, "CALL", from, to, _, amount, _, 1),
  data_flow(step2, step3),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output IntegerUnderflow

/* Transaction Order Dependency */
.decl TransactionOrderDependency(hash1:symbol, hash2:symbol, timestamp:number, block:number)
TransactionOrderDependency(hash1, hash2, timestamp, block) :-
  storage(step1, "SSTORE", block, hash1, caller1, contract, index),
  storage(step2, "SLOAD", block, hash2, caller2, contract, index),
  hash1 != hash2,
  step1 < step2,
  caller1 != caller2,
  block(block, _, _, timestamp).
.output TransactionOrderDependency

/* Unchecked Suicide */
.decl UncheckedSuicide(hash:symbol, timestamp:number, step:number, caller:Address, contract:Address, destination:Address, amount:Value)
UncheckedSuicide(hash, timestamp, step1, caller, contract, destination, amount) :-
  selfdestruct(step1, hash, caller, contract, destination, amount),
  !match("0", amount),
  !caller_check(_, hash),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  selfdestruct(step1, hash, caller, contract, destination, amount),
  !match("0", amount),
  caller_check(step2, hash),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output UncheckedSuicide

/* Unchecked Delegatecall */
.decl UncheckedDelegatecall(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address)
UncheckedDelegatecall(hash, timestamp, step2, caller, callee) :-
  opcode(step1, "CALLDATALOAD"),
  call(step2, hash, "DELEGATECALL", caller, callee, _, _, _, 1),
  data_flow(step1, step2),
  !caller_check(_, hash),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "CALLDATALOAD"),
  call(step2, hash, "DELEGATECALL", caller, callee, _, _, _, 1),
  data_flow(step1, step2),
  caller_check(step3, hash),
  step2 < step3,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output UncheckedDelegatecall

/* Parity Wallet Hack 1 */
.decl ParityWalletHack1(hash1:symbol, hash2:symbol, timestamp1:number, timestamp2:number, step:number, caller:Address, callee:Address, amount:Value)
ParityWalletHack1(hash1, hash2, timestamp1, timestamp2, step7, caller, callee, amount) :-
  call(step0, hash1, "DELEGATECALL", _, _, _, _, _, 1),
  opcode(step1, "CALLDATACOPY"),
  step0 < step1,
  storage(step2, "SSTORE", _, hash1, from, contract, index),
  data_flow(step1, step2),
  call(step3, hash2, "DELEGATECALL", _, _, _, _, _, 1),
  storage(step4, "SLOAD", _, hash2, from, contract, index),
  step3 < step4,
  step2 < step4,
  hash1 != hash2,
  caller_check(step5, hash2),
  data_flow(step4, step5),
  opcode(step6, "CALLDATALOAD"),
  call(step7, hash2, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  data_flow(step6, step7),
  step4 < step7,
  transaction(hash1, from, to, input1, 1, block1),
  block(block1, _, _, timestamp1),
  transaction(hash2, from, to, input2, 1, block2),
  block(block2, _, _, timestamp2),
  input1 != input2.
.output ParityWalletHack1

/* Parity Wallet Hack 2 */
.decl ParityWalletHack2(hash1:symbol, hash2:symbol, timestamp1:number, timestamp2:number, step:number, caller:Address, contract:Address, destination:Address, amount:Value)
ParityWalletHack2(hash1, hash2, timestamp1, timestamp2, step5, caller, contract, destination, amount) :-
  opcode(step0, "CALLDATACOPY"),
  storage(step1, "SSTORE", _, hash1, from, contract, index),
  data_flow(step0, step1),
  storage(step2, "SLOAD", _, hash2, from, contract, index),
  step1 < step2,
  hash1 != hash2,
  used_in_condition(step2, hash2),
  caller_check(step3, hash2),
  data_flow(step2, step3),
  opcode(step4, "CALLDATALOAD"),
  selfdestruct(step5, hash2, caller, contract, destination, amount),
  data_flow(step4, step5),
  step2 < step5,
  transaction(hash1, _, _, _, 1, block1),
  block(block1, _, _, timestamp1),
  transaction(hash2, _, _, _, 1, block2),
  block(block2, _, _, timestamp2).
.output ParityWalletHack2

/* Short Address */
.decl ShortAddress(hash:symbol, timestamp:number, step:number, from:Address, to:Address, amount:Value)
ShortAddress(hash, timestamp, step7, from, to, amount) :-
  transaction(hash, _, _, input_data, 1, block),
  size = strlen(input_data) / 2,
  size > 36,
  size < 68,
  opcode(step1, "CALLDATALOAD"),
  bitwise_logic(step2, "AND", _, second_operand),
  match("1461501637330902918203684832716283019655932542975", second_operand),
  data_flow(step1, step2),
  storage(step3, "SLOAD", _, hash, _, _, _),
  data_flow(step2, step3),
  arithmetic(step4, "ADD", _, _, _, _),
  data_flow(step3, step4),
  storage(step5, "SSTORE", _, hash, _, _, _),
  data_flow(step4, step5),
  opcode(step6, "CALLDATALOAD"),
  transfer(step7, hash, from, to, amount),
  data_flow(step6, step7),
  from != to,
  block(block, _, _, timestamp).
.output ShortAddress

/* DoS with Unexpected Throw */
.decl DoSWithUnexpectedThrow(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
DoSWithUnexpectedThrow(hash, timestamp, step3, caller, callee, amount) :-
  call(step1, hash, "CALL", caller, callee, _, amount, depth1, 0),
  !match("0", amount),
  throw(step2, hash, _, _, depth2),
  step1 < step2,
  depth1 < depth2,
  throw(step3, hash, _, _, depth3),
  step1 < step3,
  depth1 = depth3,
  transaction(hash, _, _, _, _, block),
  block(block, _, _, timestamp).
.output DoSWithUnexpectedThrow

/* DoS with Block Gas Limit */
.decl DoSWithBlockGasLimit(hash:symbol, timestamp:number, step:number, gas_limit:number, caller:Address, callee:Address, amount:Value)
DoSWithBlockGasLimit(hash, timestamp, step, gas_limit, caller, callee, amount) :-
  block(block, gas_used, gas_limit, timestamp),
  gas_used = gas_limit,
  transaction(hash, _, _, _, _, block),
  call(step, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  error(hash, "out of gas").
.output DoSWithBlockGasLimit

/* Timestamp Dependence */
.decl TimestampDependence(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
TimestampDependence(hash, timestamp, step2, caller, callee, amount) :-
  opcode(step1, "TIMESTAMP"),
  used_in_condition(step1, hash),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output TimestampDependence

/* Transaction State Dependence */
.decl TransactionStateDependence(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
TransactionStateDependence(hash, timestamp, step2, caller, callee, amount) :-
  opcode(step1, "ORIGIN"),
  used_in_condition(step1, hash),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, from, _, _, 1, block),
  callee != from,
  block(block, _, _, timestamp).
.output TransactionStateDependence

/* Block State Dependence */
.decl BlockStateDependence(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
BlockStateDependence(hash, timestamp, step2, caller, callee, amount) :-
  opcode(step1, "BLOCKHASH"),
  used_in_condition(step1, hash),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step2, "COINBASE"),
  used_in_condition(step1, hash),
  call(step3, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "NUMBER"),
  used_in_condition(step1, hash),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "DIFFICULTY"),
  used_in_condition(step1, hash),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp);
  opcode(step1, "GASLIMIT"),
  used_in_condition(step1, hash),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 1),
  !match("0", amount),
  step1 < step2,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output BlockStateDependence

.decl UncheckedSignature(hash:symbol, timestamp:number, step:number, input_data:symbol, from:Address, to:Address, amount:Value)
UncheckedSignature(hash, timestamp, step2, input_data, from, to, amount) :-
  opcode(step1, "SHA3"),
  call(step2, hash, "CALL", _, "0x0000000000000000000000000000000000000001", input_data, _, _, 1),
  data_flow(step1, step2),
  storage(step3, "SSTORE", _, hash, _, _, _),
  !data_flow(step1, step3),
  transfer(_, hash, from, to, amount),
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output UncheckedSignature

.decl BalanceInvariant(hash:symbol, timestamp:number, step:number)
BalanceInvariant(hash, timestamp, step2) :-
  opcode(step1, "BALANCE"),
  condition(step2, hash),
  use(step2, step1),
  throw(step3, hash, _, _, _),
  step2 < step3,
  transaction(hash, _, _, _, 1, block),
  block(block, _, _, timestamp).
.output BalanceInvariant

.decl InsufficientGas(hash:symbol, timestamp:number, step:number, caller:Address, callee:Address, amount:Value)
InsufficientGas(hash, timestamp, step2, caller, callee, amount) :-
  opcode(step1, "CALLDATALOAD"),
  call(step2, hash, "CALL", caller, callee, _, amount, _, 0),
  data_flow(step1, step2),
  error(hash, "out of gas"),
  transaction(hash, _, _, _, _, block),
  block(block, _, _, timestamp).
.output InsufficientGas
